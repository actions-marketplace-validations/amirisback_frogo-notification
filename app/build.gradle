plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {

    def appDomain = "com"
    def appDevConsole = "frogobox"
    def appName = "notification"

    def projectApplicationId = "$appDomain.$appDevConsole.$appName"

    // Declaration app name
    def nameApp = "Frogo Recycler View"
    def nameAppLower = nameApp.toLowerCase()

    // Declaration apk / aab name
    def nameAPK = nameAppLower.replace(" ", "-")

    compileSdkVersion projectCompileSdkVersion
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId projectApplicationId
        minSdkVersion projectMinSdkVersion
        targetSdkVersion projectTargetSdkVersion
        versionCode projectVersionCode
        versionName projectVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Naming APK // AAB
        setProperty("archivesBaseName", "${nameAPK}(${versionName})#${gitCurrentBranch()}")

    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

static def gitCurrentBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.4.0'

    // Library FrogoNotification
    implementation project(':frogonotification')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}